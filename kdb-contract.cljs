#!/usr/bin/env nbb
;; Contract test: fleet.kdb (datom plane) == fleet.db (EDN read model).
;; Run with the kotobase classpath (see kdb-contract.sh).
(ns kdb-contract
  (:require ["node:fs" :as fs]
            [cljs.reader :as reader]
            [kotobase-peer.core :as kb]
            [fleet.db :as fdb]
            [fleet.kdb :as kdb]
            [fleet.west :as west]
            [promesa.core :as p]))

(def edn-db (west/parse (fs/readFileSync (first *command-line-args*) "utf8")))
(def kbdb (kdb/build-db edn-db))

(def fails (atom 0))
(defn check [label a b]
  (if (= a b)
    (println "  OK  " label (str "(" (count a) ")"))
    (do (swap! fails inc)
        (println "  FAIL" label)
        (println "    edn :" (pr-str (take 5 a)))
        (println "    kdb :" (pr-str (take 5 b))))))

(println "contract: datom-plane queries == EDN-model queries")
(check "by-org kotoba-lang"
       (sort (map :repo/name (fdb/by-org edn-db "kotoba-lang")))
       (kdb/q-by-org kbdb "kotoba-lang"))
(check "by-org cloud-itonami"
       (sort (map :repo/name (fdb/by-org edn-db "cloud-itonami")))
       (kdb/q-by-org kbdb "cloud-itonami"))
(check "heavy repos"
       (sort (map :repo/name (fdb/heavy edn-db)))
       (kdb/q-heavy kbdb))
(check "datalad group"
       (sort (map :repo/name (fdb/by-group edn-db "datalad")))
       (kdb/q-by-group kbdb "datalad"))
(let [nm "kotoba-fleet-vcs"]
  (check (str "revision " nm)
         [(:repo/revision (west/find-repo edn-db nm))]
         [(kdb/q-revision kbdb nm)]))
(check "repo count"
       [(count (:fleet/repos edn-db))]
       [(kdb/q-count kbdb)])

;; persistence round-trip (content-addressed chain)
(println "persistence: transact -> commit! -> hydrate -> query")
(let [{:keys [put! get-fn]} (kdb/mem-store)
      tx (mapcat kdb/repo->tx (take 3 (:fleet/repos edn-db)))]
  (-> (kb/commit! put! get-fn tx nil kdb/id-encrypt)
      (p/then (fn [chain-cid]
                (p/let [rows (kb/hot-datoms get-fn chain-cid (constantly true)
                                            kdb/id-blind kdb/id-decrypt)]
                  (println "  chain-cid:" (subs (str chain-cid) 0 24) "...")
                  (println "  hydrated datoms:" (count rows)
                           (if (pos? (count rows)) "OK" "FAIL"))
                  (when (zero? (count rows)) (swap! fails inc))
                  (println (if (zero? @fails) "\nALL GREEN" "\nFAILURES"))
                  (when (pos? @fails) (js/process.exit 1)))))
      (p/catch (fn [e] (println "persist error:" (str e)) (js/process.exit 1)))))
